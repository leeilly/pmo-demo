
buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
        targetDir = 'target'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'com.pulmuone.demo'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }


    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly ('org.projectlombok:lombok')
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        compile 'mysql:mysql-connector-java'
        compile ('org.apache.commons:commons-collections4:4.1')
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
        compile group: 'org.apache.commons', name: 'commons-jcs-core', version: '2.0'

        compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
        compile group: 'commons-validator', name: 'commons-validator', version: '1.6'

        compile group: 'commons-io', name: 'commons-io', version: '2.6'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

// jar 플젝 구성
configure(subprojects.findAll {it.name == 'pmo-demo-common'} ) {
    println "jar configure project name is : $it.name : " + it.name + " : " + project.name

    jar {
        enabled = true
        baseName = project.name
        destinationDir = file("$targetDir")
    }

    bootJar {
        enabled = false
    }

}

// war 플젝 구성
configure(subprojects.findAll {it.name == 'pmo-demo-api'} ) {
    println "configure project name is : $it.name : " + it.name + " : " + project.name

    apply plugin: 'war'
    war {
        baseName = project.name
        destinationDir = file("$targetDir")
    }

    configurations {
        customProvidedRuntime
    }
    dependencies {
        // embeded tomcat
        customProvidedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    }
}

project(':pmo-demo-common') {
    jar {
        baseName = project.name
    }

    dependencies {
        compile group: 'com.navercorp.lucy', name: 'lucy-xss', version: '1.6.3'
    }
}

project(':pmo-demo-api') {
    dependencies {
        compile project(':pmo-demo-common')

        //elastic
        compile group: 'org.elasticsearch', name: 'elasticsearch', version: '7.0.0'
        compile group: 'org.elasticsearch.client', name: 'elasticsearch-rest-high-level-client', version: '7.0.0'

        compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
        //swagger
        compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
        compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'

        // mybatis
        compile group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '1.3.1'

        // lucy xss servlet filter
        compile group: 'com.navercorp.lucy', name: 'lucy-xss-servlet', version: '2.0.0'

    }
}
